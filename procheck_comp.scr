#!/bin/csh
#++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
#  procheck_comp.scr
#  -----------------
#  Shell script for running PROCHECK comparisons on aligned
#  structures
#  Roman Laskowski, November 1994
#  Current version: PROCHECK v.3.5.4, Feb 2001
#
#++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
#
# Check that exactly two parameters have been entered
#if ($#argv != 1 && $#argv != 2) then
if ($#argv != 1) then
  echo ' '
  echo '*** ERROR. Correct usage is:'
  echo ' '
#  echo '           procheck_comp  filelist  [alignment]'
  echo '           procheck_comp  filelist '
  echo ' '
  echo ' '
  echo '    where  filelist     = file containing list of structures'
  echo '                          to be compared'
#  echo '           [alignment]  = alignments file (optional) - not yet'
#  echo '                          implemented'
  echo ' '
  echo 'For example:-'
  echo ' '
#  echo '           procheck_comp globins.list globins.align'
  echo '           procheck_comp globins.list'
  echo ' '
else
  echo ' '
  echo ' '
  echo 'PROCHECK_COMP. PROCHECK v.3.5.4 (Feb 2001)'
  echo '------------------------------------------'
  echo ' '
  set filelist = $1
  echo 'Coordinates file:   [' $filelist ']'
  echo ' '
  if ($#argv == 1) then
    set alignment = ' '
    echo ' '
#    echo '* No alignment file supplied. Comparison will be based on residue numbers'
    echo ' '
  else
    set alignment = $2
    echo 'Alignment file:    [' $alignment ']'
    echo ' '
  endif
  echo ' '
  echo 'Program directory:   ' $prodir
  echo ' '
  set ensemb_name = '%'$filelist
#
# If the file-list exists, then do the business
#
if (-e $filelist) then

    #-------------#
    # C L E A N   #
    #-------------#
    echo 'CLEAN - Clean-up program' >! clean.log
    echo '------------------------' >> clean.log
    echo ' ' >> clean.log
    echo 'Running clean-up on file: '$filelist
    echo ' '
    if (-e resdefs.dat) then
    else
      cp $prodir/resdefs.data resdefs.dat
    endif
    $prodir/clean >> clean.log << EOD
$ensemb_name
EOD
    grep "*" clean.log
    echo ' '
    echo '.................................................................'
    #-------------#
    # S E C S T R #
    #-------------#
    echo ' ' >! secstr.log
    echo 'SECSTR - Secondary structure assignment' >> secstr.log
    echo '---------------------------------------' >> secstr.log
    echo ' ' >> secstr.log
    echo ' '
    echo 'Secondary structure assignment'
    echo ' '
    $prodir/secstr >> secstr.log << EOD
$ensemb_name
EOD
    grep "*" secstr.log
    echo ' '
    echo '..................................................................'
    #-------------#
    # R M S D E V #
    #-------------#
    echo ' ' >! rmsdev.log
    echo 'RMSDEV - RMS deviations for ensemble' >> rmsdev.log
    echo '------------------------------------' >> rmsdev.log
    echo ' ' >> rmsdev.log
    echo ' '
    echo 'RMS deviations for ensemble'
    echo ' '
    $prodir/rmsdev >> rmsdev.log << EOD
$ensemb_name
EOD
    grep "*" rmsdev.log
    echo ' '
    echo '..................................................................'
    #-------------#
    # T P L O T   #
    #-------------#
    if (-e prodata) then
    else
      cp $prodir/procheck.dat prodata
    endif
    if (-e procheck_comp.prm) then
    else
      cp $prodir/procheck_comp.prm .
    endif
    echo ' ' >! tplot.log
    echo 'Phi-psi and chi1-chi2 distributions' >> tplot.log
    echo '-----------------------------------' >> tplot.log
    echo ' ' >> tplot.log
    echo ' '
    echo 'Phi-psi and chi1-chi2 distributions'
    echo ' '
    $prodir/tplot >> tplot.log << EOD
$ensemb_name

0.0
Y
EOD
    echo ' '
    grep "*" tplot.log
    echo ' '
    echo '..................................................................'
    #-------------#
    # M P L O T   #
    #-------------#
    echo ' ' >! mplot.log
    echo 'Dihedral angle distribution plots and quality plots' >> mplot.log
    echo '---------------------------------------------------' >> mplot.log
    echo ' ' >> mplot.log
    echo ' '
    echo 'Dihedral angle distributions and quality plots'
    echo ' '
    $prodir/mplot >> mplot.log << EOD
$ensemb_name


EOD
    echo ' '
    grep "*" mplot.log
    echo ' '
    echo '..................................................................'
  #
  # Tidy up at end
  # --------------
    if (-e resdefs.dat) then
      \rm resdefs.dat
    endif
    if (-e prodata) then
      \rm prodata
    endif
else
    echo 'File not found: '$filelist
endif
endif

exit(0)

